project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'
project.ext.DATA_DIR = projectDir.toString() + '/../../data/cally'

apply plugin: 'com.android.application'

android {
    compileSdkVersion = 25
    buildToolsVersion = '25.0.2'

    defaultConfig {
        minSdkVersion   14
        targetSdkVersion  25
        ndk {
            abiFilters 'armeabi-v7a' //'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-14',
                        '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=gnustl_static'
            }
        }
    }

    sourceSets {
        main {
            assets.srcDirs = [project.ext.ASSET_DIR]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

task copyAssets(type: Copy) {
    from project.ext.DATA_DIR
    into project.ext.ASSET_DIR + '/cally'
}

afterEvaluate {
    // create dir if not exists
    if (!(new File(project.ext.ASSET_DIR)).exists()) {
        new File(project.ext.ASSET_DIR).mkdirs();
    }

    assembleDebug.dependsOn copyAssets
    assembleRelease.dependsOn copyAssets
}
